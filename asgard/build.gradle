import nebula.core.tasks.Download
task download(type: Download) {
    downloadBase = 'https://github.com/Netflix/asgard/releases/download/asgard-1.5'
    downloadFileName = 'asgard.war'
}

// TODO tomcat_extra_opts: "-Darchaius.deployment.applicationId=asgard -Dnetflix.datacenter=cloud"
import org.apache.tools.ant.filters.*
ospackage {
    from(download) {
        into('/var/lib/tomcat7/webapps/')
        rename(/.*/, 'ROOT.war')
    }
    from(file('root')) {
        into('/')
        user = 'tomcat7'
        permissionGroup = 'tomcat7'

        def accountNumber = System.getenv('EC2_OWNER_ID')

        // It'd be nice to alert on this, but it seems like every run requires this
        //if (!accountNumber) {
        //    throw new GradleException("Please run eval \$(baseami/root/usr/local/bin/metadatavars) first")
        //}
        filter ReplaceTokens, tokens: [
                "ASGARD_PASSWORD": project.property("asgard.password"),
                "ACCOUNT_NUMBER": accountNumber
        ]
    }
    postInstall('mv /etc/tomcat7/tomcat-users.xml.asgard /etc/tomcat7/tomcat-users.xml')
    postInstall('echo "export CATALINA_OPTS=\\"\\$CATALINA_OPTS -DonlyRegions=us-west-2\\"" >> /usr/share/tomcat7/bin/setenv.sh')

    def eureka = System.getenv("EUREKA_ELB")
    if (eureka) {
        println "Provide Eureka Configuration for us-west-2"
        postInstall("echo 'eureka.regionsToServers = [(Region.US_WEST_2): \"http://${eureka}\"]' >> /usr/share/tomcat7/.asgard/Config.groovy")
    }
}